
plugins {
    id 'java'
    id 'groovy'
}

group 'org.korizza.colorizer'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task makeNativeObj(type: Exec) {
    workingDir './src/main/jni'
    commandLine 'make'
}
classes.dependsOn(makeNativeObj)

task cleanJniBuild(type: Exec) {
    workingDir './src/main/jni'
    commandLine 'make', 'clean'
}
clean.dependsOn(cleanJniBuild)

task makeJniHeaders(type: Exec) {
    workingDir './src/main/jni'
    commandLine 'make', 'jni_headers'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.korizza.colorizer.demo.App'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }        
    }
    baseName = project.name
}


dependencies {
    compile group: 'log4j', name: 'log4j', version: '1.2.17'    
    compile group: 'com.google.guava', name: 'guava', version: '23.3-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


